#version 450

#define N_THREADS 256

layout(local_size_x = N_THREADS, local_size_y = 1, local_size_z = 1) in;

struct BatchInfo {
    uint inputOffset;
    uint inputSizeX;
    uint outputOffset;
    uint outputSizeX;
};

layout(binding = 0) readonly buffer inputBuffer { float x[]; };
layout(binding = 1) writeonly buffer outputBuffer { float y[]; };
layout(binding = 2) readonly buffer batchInfosBuffer { BatchInfo infos[]; };
layout(binding = 3) readonly uniform configBuffer {
    uint indexPipeIndex;
};
layout(binding = 4) readonly buffer indexBuffer { float indexes[]; };

shared uint sharedDim;
shared uint sharedXOffset;
shared uint sharedYOffset;

void main() {
    const uint threadIndex = gl_LocalInvocationID.x;

    if (threadIndex == 0) {
        const uint nWorkGroups = gl_NumWorkGroups.z;
        const uint batchIndex = gl_WorkGroupID.y;
        const uint workGroupIndex = gl_WorkGroupID.z;

        const uint index = uint(indexes[batchIndex]);
        BatchInfo info = infos[batchIndex];
        sharedDim = info.inputSizeX / nWorkGroups;
        const uint dimOffset = sharedDim * workGroupIndex;
        sharedXOffset = info.inputOffset + dimOffset;
        sharedYOffset = index * info.inputSizeX + dimOffset;
    }

    barrier();
    memoryBarrierShared();

    const uint dim = sharedDim;
    const uint xOffset = sharedXOffset;
    const uint yOffset = sharedYOffset;

    for (uint i = threadIndex; i < dim; i += N_THREADS) {
        y[yOffset + i] = x[xOffset + i];
    }
}
