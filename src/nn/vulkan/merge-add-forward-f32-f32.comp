#version 450

#extension GL_EXT_control_flow_attributes : enable

#define N_THREADS 256

layout(local_size_x = N_THREADS, local_size_y = 1, local_size_z = 1) in;

struct BatchInfo {
    uint inputOffset;
    uint inputSizeX;
    uint outputOffset;
    uint outputSizeX;
};

layout(binding = 0) readonly buffer inputBuffer { float x[]; };
layout(binding = 1) writeonly buffer outputBuffer { float y[]; };
layout(binding = 2) readonly buffer batchInfosBuffer { BatchInfo infos[]; };

shared BatchInfo sharedInfo;

void main() {
    const uint threadIndex = gl_LocalInvocationID.x;
    const uint batchIndex = gl_GlobalInvocationID.y;

    if (threadIndex == 0) {
        sharedInfo = infos[batchIndex];
    }
    memoryBarrierShared();
    barrier();

    const uint inputSizeX = sharedInfo.inputSizeX;
    const uint inputOffset = sharedInfo.inputOffset;
    const uint outputOffset = sharedInfo.outputOffset;
    const uint outputSizeX = sharedInfo.outputSizeX;
    const uint nNodes = inputSizeX / outputSizeX;

    for (uint i = threadIndex; i < outputSizeX; i += N_THREADS) {
        float sum = 0.0;
        const uint iOffset = inputOffset + i;
        const uint oOffset = outputOffset + i;
        for (uint n = 0; n < nNodes; n++) {
            sum += x[n * outputSizeX + iOffset];
        }
        y[oOffset] += sum;
    }
}
