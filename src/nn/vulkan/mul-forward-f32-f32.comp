#version 450

#define N_THREADS 64

layout(local_size_x = N_THREADS, local_size_y = 1, local_size_z = 1) in;

struct BatchInfo {
    uint inputOffset;
    uint inputSizeX;
    uint outputOffset;
    uint outputSizeX;
};

layout(binding = 0) readonly buffer inputBuffer { float x[]; };
layout(binding = 1) buffer outputBuffer { float y[]; };
layout(binding = 2) readonly buffer batchInfosBuffer { BatchInfo infos[]; };
layout(binding = 3) readonly uniform configBuffer {
    uint multiplierBufferIndex;
};
layout(binding = 4) readonly buffer multiplierBuffer { float m[]; };

shared uint sharedDim;
shared uint sharedXyOffset;
shared uint sharedMOffset;

void main() {
    const uint threadIndex = gl_LocalInvocationID.x;

    if (threadIndex == 0) {
        const uint nWorkGroups = gl_NumWorkGroups.z;
        const uint batchIndex = gl_WorkGroupID.y;
        const uint workGroupIndex = gl_WorkGroupID.z;

        const BatchInfo info = infos[batchIndex];
        sharedDim = info.inputSizeX / nWorkGroups;
        sharedXyOffset = info.inputOffset + sharedDim * workGroupIndex;
        sharedMOffset = info.inputSizeX * batchIndex + sharedDim * workGroupIndex;
    }

    barrier();
    memoryBarrierShared();

    const uint dim = sharedDim;
    const uint xyOffset = sharedXyOffset;
    const uint mOffset = sharedMOffset;

    for (uint i = threadIndex; i < dim; i += N_THREADS) {
        y[xyOffset + i] = x[xyOffset + i] * m[mOffset + i];
    }
}
