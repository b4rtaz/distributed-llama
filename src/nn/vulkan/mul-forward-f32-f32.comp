#version 450

#define N_THREADS 256
#define N_BATCHES 32

layout(local_size_x = N_THREADS, local_size_y = 1, local_size_z = 1) in;

struct BatchInfo {
    uint inputOffset;
    uint inputSizeX;
    uint outputOffset;
    uint outputSizeX;
};

layout(binding = 0) readonly buffer inputBuffer { float x[]; };
layout(binding = 1) buffer outputBuffer { float y[]; };
layout(binding = 2) readonly uniform batchInfosBuffer { BatchInfo infos[N_BATCHES]; };
layout(binding = 3) readonly uniform configBuffer {
    uint multiplierBufferIndex;
};
layout(binding = 4) readonly buffer multiplierBuffer { float m[]; };

shared uint sharedDim;
shared uint sharedXyOffset;
shared uint sharedMOffset;

void main() {
    const uint threadIndex = gl_LocalInvocationID.x;

    if (threadIndex == 0) {
        const uint nWorkGroups = gl_NumWorkGroups.z;
        const uint batchIndex = gl_WorkGroupID.y;
        const uint workGroupIndex = gl_WorkGroupID.z;

        const BatchInfo info = infos[batchIndex];
        const uint slice = info.inputSizeX / nWorkGroups;
        const uint rest = info.inputSizeX % nWorkGroups;
        const uint offset = workGroupIndex * slice + min(rest, workGroupIndex);

        sharedDim = slice + (workGroupIndex < rest ? 1 : 0);
        sharedXyOffset = info.inputOffset + offset;
        sharedMOffset = info.inputSizeX * batchIndex + offset;
    }

    barrier();

    const uint dim = sharedDim;
    const uint xyOffset = sharedXyOffset;
    const uint mOffset = sharedMOffset;

    for (uint i = threadIndex; i < dim; i += N_THREADS) {
        y[xyOffset + i] = x[xyOffset + i] * m[mOffset + i];
    }
}
