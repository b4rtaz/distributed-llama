#version 450

#extension GL_EXT_control_flow_attributes : enable
#extension GL_EXT_shader_16bit_storage : enable
#extension GL_EXT_shader_explicit_arithmetic_types : enable

#define N_THREADS 64
#define TILE_SIZE_X 2
#define TILE_SIZE_D 16

#define Q80_Q40_BLOCK_SIZE 32

layout(local_size_x = N_THREADS, local_size_y = 1, local_size_z = 1) in;

struct BatchInfo {
    uint inputOffset;
    uint inputSizeX;
    uint outputOffset;
    uint outputSizeX;
};

struct BlockQ80 {
    float16_t d;
    int8_t qs[Q80_Q40_BLOCK_SIZE];
};

struct BlockQ40 {
    float16_t d;
    uint8_t qs[Q80_Q40_BLOCK_SIZE / 2];
};

layout(binding = 0) readonly buffer inputBuffer { BlockQ80 x[]; };
layout(binding = 1) writeonly buffer outputBuffer { float y[]; };
layout(binding = 2) readonly buffer batchInfosBuffer { BatchInfo infos[]; };
layout(binding = 3) readonly buffer weightBuffer { BlockQ40 weight[]; };

shared uint sharedXSlice;
shared uint sharedXRest;
shared uint sharedInputOffset;
shared uint sharedInputSizeX;
shared uint sharedOutputOffset;
shared uint sharedD;
shared float16_t sums[N_THREADS * TILE_SIZE_D];

void main() {
    const uint threadIndex = gl_LocalInvocationID.x;

    if (threadIndex == 0) {
        const uint batchIndex = gl_WorkGroupID.y;
        const uint workGroupIndex = gl_WorkGroupID.z;

        const BatchInfo info = infos[batchIndex];

        const uint xTiles = info.inputSizeX / TILE_SIZE_X;
        sharedXSlice = xTiles / N_THREADS;
        sharedXRest = xTiles % N_THREADS;

        sharedInputOffset = info.inputOffset;
        sharedInputSizeX = info.inputSizeX;
        sharedOutputOffset = info.outputOffset;
        sharedD = TILE_SIZE_D * workGroupIndex;
    }

    barrier();
    memoryBarrierShared();

    const uint xSlice = sharedXSlice;
    const uint xRest = sharedXRest;
    const uint xStart = (threadIndex * xSlice + min(threadIndex, xRest)) * TILE_SIZE_X;
    const uint xEnd = xStart + (xSlice + (threadIndex < xRest ? 1 : 0)) * TILE_SIZE_X;

    const uint inputOffset = sharedInputOffset;
    const uint inputSizeX = sharedInputSizeX;
    const uint outputOffset = sharedOutputOffset;
    const uint d = sharedD;

    f16vec4 xTemp[Q80_Q40_BLOCK_SIZE / 4];

    for (uint dt = 0; dt < TILE_SIZE_D; dt++) {
        sums[threadIndex * TILE_SIZE_D + dt] = float16_t(0.0f);
    }

    for (uint i = xStart; i < xEnd; i += TILE_SIZE_X) {
        [[unroll]] for (uint it = 0; it < TILE_SIZE_X; it++) {
            const uint xi = inputOffset + i + it;
            const float16_t xScale = x[xi].d;
            [[unroll]] for (uint j = 0; j < Q80_Q40_BLOCK_SIZE / 4; j++) {
                xTemp[j] = f16vec4(
                    x[xi].qs[j * 2],
                    x[xi].qs[j * 2 + Q80_Q40_BLOCK_SIZE / 2],
                    x[xi].qs[j * 2 + 1],
                    x[xi].qs[j * 2 + 1 + Q80_Q40_BLOCK_SIZE / 2]
                );
            }

            [[unroll]] for (uint dt = 0; dt < TILE_SIZE_D; dt++) {
                const uint wi = (d + dt) * inputSizeX + (i + it);
                const BlockQ40 wBlock = weight[wi];

                float16_t s = float16_t(0);
                [[unroll]] for (uint j = 0; j < Q80_Q40_BLOCK_SIZE / 4; j++) {
                    uint w0 = wBlock.qs[j * 2];
                    uint w1 = wBlock.qs[j * 2 + 1];
                    ivec4 w = ivec4(
                        w0 & 0xFu,
                        w0 >> 4,
                        w1 & 0xFu,
                        w1 >> 4
                    ) - ivec4(8);
                    s += dot(xTemp[j], f16vec4(w));
                }
                sums[threadIndex * TILE_SIZE_D + dt] += s * xScale * wBlock.d;
            }
        }
    }

    barrier();
    memoryBarrierShared();

    [[unroll]] for (uint i = N_THREADS / 2; i > 0; i >>= 1) {
        for (uint dt = 0; dt < TILE_SIZE_D; dt++) {
            if (threadIndex < i) {
                sums[threadIndex * TILE_SIZE_D + dt] += sums[(threadIndex + i) * TILE_SIZE_D + dt];
            }
        }
        barrier();
    }
    for (uint dt = threadIndex; dt < TILE_SIZE_D; dt += N_THREADS) {
        y[outputOffset + d + dt] = float(sums[dt]);
    }
}
