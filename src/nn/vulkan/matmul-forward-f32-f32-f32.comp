#version 450

#define N_THREADS 512
#define N_BATCHES 32

layout(local_size_x = N_THREADS, local_size_y = 1, local_size_z = 1) in;

struct BatchInfo {
    uint inputOffset;
    uint inputSizeX;
    uint outputOffset;
    uint outputSizeX;
};

layout(binding = 0) readonly buffer inputBuffer { float x[]; };
layout(binding = 1) writeonly buffer outputBuffer { float y[]; };
layout(binding = 2) readonly uniform batchInfosBuffer { BatchInfo infos[N_BATCHES]; };
layout(binding = 3) readonly buffer weightBuffer { float weight[]; };

void main() {
    const uint threadIndex = gl_LocalInvocationID.x;
    const uint batchIndex = gl_WorkGroupID.y;

    const BatchInfo sharedInfo = infos[batchIndex];
    const uint xDim = sharedInfo.inputSizeX;
    const uint yDim = sharedInfo.outputSizeX;
    const uint xOffset = sharedInfo.inputOffset;
    const uint yOffset = sharedInfo.outputOffset;

    for (uint d = threadIndex; d < yDim; d += N_THREADS) {
        const uint wOffset = d * xDim;

        float sum = 0.0f;
        for (uint j = 0; j < xDim; j++) {
            sum += x[xOffset + j] * weight[wOffset + j];
        }
        y[yOffset + d] = sum;
    }
}
