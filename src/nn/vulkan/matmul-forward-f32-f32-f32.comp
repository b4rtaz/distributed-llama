#version 450

#define N_WORK_GROUPS 32
#define N_THREADS 256

layout(local_size_x = N_THREADS, local_size_y = 1, local_size_z = 1) in;

struct BatchInfo {
    uint inputOffset;
    uint inputSizeX;
    uint outputOffset;
    uint outputSizeX;
};

layout(binding = 0) readonly buffer inputBuffer { float x[]; };
layout(binding = 1) writeonly buffer outputBuffer { float y[]; };
layout(binding = 2) readonly buffer batchInfosBuffer { BatchInfo infos[]; };
layout(binding = 3) readonly buffer weightBuffer { float weight[]; };

shared BatchInfo sharedInfo;

void main() {
    const uint threadIndex = gl_LocalInvocationID.x;
    const uint workGroupIndex = gl_GlobalInvocationID.z;
    const uint batchIndex = gl_GlobalInvocationID.y;

    if (threadIndex == 0) {
        sharedInfo = infos[batchIndex];
    }

    barrier();
    memoryBarrierShared();

    const uint inputSizeX = sharedInfo.inputSizeX;
    const uint dim = sharedInfo.outputSizeX / N_WORK_GROUPS;
    const uint xOffset = sharedInfo.inputOffset;
    const uint yOffset = sharedInfo.outputOffset;

    for (uint i = threadIndex; i < dim; i += N_THREADS) {
        const uint d = (workGroupIndex * dim) + i;
        const uint wOffset = d * inputSizeX;

        float sum = 0.0;
        for (uint j = 0; j < inputSizeX; j++) {
            sum += x[xOffset + j] * weight[wOffset + j];
        }
        y[yOffset + d] = sum;
    }
}
