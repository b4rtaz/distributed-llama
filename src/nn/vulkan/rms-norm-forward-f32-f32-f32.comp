#version 450

#define N_THREADS 128

layout(local_size_x = N_THREADS, local_size_y = 1, local_size_z = 1) in;

struct BatchInfo {
    uint inputOffset;
    uint inputSizeX;
    uint outputOffset;
    uint outputSizeX;
};

layout(binding = 0) readonly buffer inputBuffer { float x[]; };
layout(binding = 1) writeonly buffer outputBuffer { float y[]; };
layout(binding = 2) readonly buffer batchInfosBuffer { BatchInfo infos[]; };
layout(binding = 3) readonly buffer weightBuffer { float weight[]; };
layout(binding = 4) readonly uniform configBuffer {
    uint invRmsBufferIndex; // not used
    uint nColumns;
};
layout(binding = 5) readonly buffer invRmsBuffer { float invRms[]; };

shared uint sharedDim;
shared uint sharedDimOffset;
shared uint sharedColDim;
shared uint sharedXOffset;
shared uint sharedYOffset;

void main() {
    const uint threadIndex = uint(gl_LocalInvocationID.x);
    const uint batchIndex = gl_WorkGroupID.y;

    if (threadIndex == 0) {
        const uint nWorkGroups = gl_NumWorkGroups.z;
        const uint workGroupIndex = gl_WorkGroupID.z;

        const BatchInfo info = infos[batchIndex];
        sharedDim = info.inputSizeX / nWorkGroups;
        sharedDimOffset = sharedDim * workGroupIndex;
        sharedColDim = info.inputSizeX / nColumns;
        sharedXOffset = info.inputOffset + sharedDimOffset;
        sharedYOffset = info.outputOffset + sharedDimOffset;
    }

    barrier();
    memoryBarrierShared();

    const uint dim = sharedDim;
    const uint dimOffset = sharedDimOffset;
    const uint colDim = sharedColDim;
    const uint xOffset = sharedXOffset;
    const uint yOffset = sharedYOffset;
    const uint invRmsOffset = batchIndex * nColumns;

    for (uint i = threadIndex; i < dim; i += N_THREADS) {
        const uint j = i + dimOffset;
        const float s = invRms[invRmsOffset + (j / colDim)];
        y[yOffset + i] = (x[xOffset + i] * s) * weight[j % colDim];
    }
}
