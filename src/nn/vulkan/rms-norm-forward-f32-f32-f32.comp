#version 450

#define N_THREADS 256
#define N_BATCHES 32

layout(local_size_x = N_THREADS, local_size_y = 1, local_size_z = 1) in;

struct BatchInfo {
    uint inputOffset;
    uint inputSizeX;
    uint outputOffset;
    uint outputSizeX;
};

layout(binding = 0) readonly buffer inputBuffer { float x[]; };
layout(binding = 1) writeonly buffer outputBuffer { float y[]; };
layout(binding = 2) readonly uniform batchInfosBuffer { BatchInfo infos[N_BATCHES]; };
layout(binding = 3) readonly buffer weightBuffer { float weight[]; };
layout(binding = 4) readonly uniform configBuffer {
    uint invRmsBufferIndex; // not used
    uint nColumns;
};
layout(binding = 5) readonly buffer invRmsBuffer { float invRms[]; };

void main() {
    const uint threadIndex = uint(gl_LocalInvocationID.x);
    const uint batchIndex = gl_WorkGroupID.y;
    const uint colIndex = gl_WorkGroupID.z;

    const BatchInfo info = infos[batchIndex];
    const uint dim = info.inputSizeX / nColumns;
    const uint offset = dim * colIndex;
    const uint xOffset = info.inputOffset + offset;
    const uint yOffset = info.outputOffset + offset;

    const float s = invRms[batchIndex * nColumns + colIndex];

    for (uint i = threadIndex; i < dim; i += N_THREADS) {
        y[yOffset + i] = (x[xOffset + i] * s) * weight[i];
    }
}
